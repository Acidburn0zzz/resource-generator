##################################################################################
# Document Properties Section

SET DOCUMENT Name = "BEL Framework Small Corpus Document"
SET DOCUMENT Description = "Approximately 2000 hand curated statements drawn from 57 PubMeds."
SET DOCUMENT Version = "1.6"
SET DOCUMENT Copyright = "Copyright (c) 2011-2012, Selventa. All Rights Reserved."
SET DOCUMENT Authors = "Selventa"
SET DOCUMENT Licenses = "Creative Commons Attribution-Non-Commercial-ShareAlike 3.0 Unported License"
SET DOCUMENT ContactInfo = "support@belframework.org"

##################################################################################
# Definitions Section

DEFINE NAMESPACE SDIS AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-legacy-diseases.belns"
DEFINE NAMESPACE PFR AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-rat-protein-families.belns"
DEFINE NAMESPACE MGI AS URL "http://resource.belframework.org/belframework/1.0/namespace/mgi-approved-symbols.belns"
DEFINE NAMESPACE PFM AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-mouse-protein-families.belns"
DEFINE NAMESPACE MESHPP AS URL "http://resource.belframework.org/belframework/1.0/namespace/mesh-biological-processes.belns"
DEFINE NAMESPACE MESHCL AS URL "http://resource.belframework.org/belframework/1.0/namespace/mesh-cellular-locations.belns"
DEFINE NAMESPACE NCH AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-human-complexes.belns"
DEFINE NAMESPACE CHEBI AS URL "http://resource.belframework.org/belframework/1.0/namespace/chebi-names.belns"
DEFINE NAMESPACE NCR AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-rat-complexes.belns"
DEFINE NAMESPACE HGNC AS URL "http://resource.belframework.org/belframework/1.0/namespace/hgnc-approved-symbols.belns"
DEFINE NAMESPACE SPAC AS URL "http://resource.belframework.org/belframework/1.0/namespace/swissprot-accession-numbers.belns"
DEFINE NAMESPACE PFH AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-human-protein-families.belns"
DEFINE NAMESPACE NCM AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-mouse-complexes.belns"
DEFINE NAMESPACE CHEBIID AS URL "http://resource.belframework.org/belframework/1.0/namespace/chebi-ids.belns"
DEFINE NAMESPACE RGD AS URL "http://resource.belframework.org/belframework/1.0/namespace/rgd-approved-symbols.belns"
DEFINE NAMESPACE MESHD AS URL "http://resource.belframework.org/belframework/1.0/namespace/mesh-diseases.belns"
DEFINE NAMESPACE GO AS URL "http://resource.belframework.org/belframework/1.0/namespace/go-biological-processes-names.belns"
DEFINE NAMESPACE SCHEM AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-legacy-chemical-names.belns"
DEFINE NAMESPACE EGID AS URL "http://resource.belframework.org/belframework/1.0/namespace/entrez-gene-ids-hmr.belns"

DEFINE ANNOTATION HemicAndImmuneSystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-hemic-and-immune-system.belanno"
DEFINE ANNOTATION UrogenitalSystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-urogenital-system.belanno"
DEFINE ANNOTATION FluidAndSecretion AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-fluid-and-secretion.belanno"
DEFINE ANNOTATION CellStructure AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-cell-structure.belanno"
DEFINE ANNOTATION CellLine AS  URL "http://resource.belframework.org/belframework/1.0/annotation/atcc-cell-line.belanno"
DEFINE ANNOTATION TextLocation AS  LIST {"Abstract","Results","Legend","Review"}
DEFINE ANNOTATION IntegumentarySystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-integumentary-system.belanno"
DEFINE ANNOTATION DigestiveSystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-digestive-system.belanno"
DEFINE ANNOTATION MuskuloskeletalSystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-musculoskeletal-system.belanno"
DEFINE ANNOTATION Tissue AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-tissue.belanno"
DEFINE ANNOTATION BodyRegion AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-body-region.belanno"
DEFINE ANNOTATION Cell AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-cell.belanno"
DEFINE ANNOTATION NervousSystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-nervous-system.belanno"
DEFINE ANNOTATION SenseOrgan AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-sense-organ.belanno"
DEFINE ANNOTATION Disease AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-disease.belanno"
DEFINE ANNOTATION RespiratorySystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-respiratory-system.belanno"
DEFINE ANNOTATION Species AS  URL "http://resource.belframework.org/belframework/1.0/annotation/species-taxonomy-id.belanno"
DEFINE ANNOTATION CardiovascularSystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-cardiovascular-system.belanno"
DEFINE ANNOTATION EndocrineSystem AS  URL "http://resource.belframework.org/belframework/1.0/annotation/mesh-endocrine-system.belanno"

##################################################################################
# Statements Section

SET STATEMENT_GROUP = "Group 1"

SET Disease = "Atherosclerosis"
SET CardiovascularSystem = "Arteries"
SET TextLocation = "Review"
SET Citation = {"PubMed","Trends in molecular medicine","12928037","","de Nigris F|Lerman A|Ignarro LJ|Williams-Ignarro S|Sica V|Baker AH|Lerman LO|Geng YJ|Napoli C",""}

pathology(MESHD:Atherosclerosis) positiveCorrelation biologicalProcess(GO:"lipid oxidation")
pathology(MESHD:Atherosclerosis) positiveCorrelation biologicalProcess(GO:"protein oxidation")

biologicalProcess(GO:"response to oxidative stress") increases biologicalProcess(GO:"apoptotic process")
biologicalProcess(GO:"response to oxidative stress") increases biologicalProcess(GO:necrosis)

SET Cell = "Endothelial Cells"

abundance(SCHEM:"Oxidized Low Density Lipoprotein") increases biologicalProcess(GO:"apoptotic process")

SET MuskuloskeletalSystem = "Muscle, Smooth, Vascular"

abundance(SCHEM:"Oxidized Low Density Lipoprotein") increases biologicalProcess(GO:"apoptotic process")
abundance(CHEBI:"oxygen radical") increases abundance(SCHEM:"Oxidized Low Density Lipoprotein")

abundance(CHEBI:"reactive oxygen species") increases biologicalProcess(GO:"apoptotic process")

abundance(CHEBI:"angiotensin II") increases biologicalProcess(GO:"apoptotic process")

biologicalProcess(GO:aging) positiveCorrelation pathology(MESHD:"Plaque, Atherosclerotic")
biologicalProcess(GO:aging) increases biologicalProcess(GO:"apoptotic process")

biologicalProcess(GO:"response to fluid shear stress") increases rnaAbundance(HGNC:NOS3)
biologicalProcess(GO:"response to fluid shear stress") increases catalyticActivity(proteinAbundance(HGNC:NOS3))
catalyticActivity(proteinAbundance(HGNC:NOS3)) directlyIncreases abundance(CHEBI:"nitric oxide")
abundance(CHEBI:"nitric oxide") decreases biologicalProcess(GO:"apoptotic process")

abundance(CHEBI:"L-arginine") decreases pathology(MESHD:Atherosclerosis)

SET Tissue = "Endothelium, Vascular"

abundance(SCHEM:"Oxidized Low Density Lipoprotein") increases abundance(CHEBI:"reactive oxygen species")
abundance(SCHEM:"Oxidized Low Density Lipoprotein") decreases proteinAbundance(HGNC:SOD1)
abundance(SCHEM:"Oxidized Low Density Lipoprotein") decreases proteinAbundance(PFH:"GPX Family")

SET CellLine = "U-937"

abundance(SCHEM:"7-Ketocholesterol") increases biologicalProcess(GO:"apoptotic process")
abundance(SCHEM:"7-Ketocholesterol") negativeCorrelation abundance(CHEBI:antioxidant)
abundance(CHEBI:antioxidant) decreases (abundance(SCHEM:"7-Ketocholesterol") increases biologicalProcess(GO:"apoptotic process"))

SET Tissue = "Muscle, Smooth, Vascular"

abundance(CHEBI:"hydrogen peroxide") increases biologicalProcess(GO:"apoptotic process")
proteinAbundance(HGNC:CAT) decreases (abundance(CHEBI:"hydrogen peroxide") increases biologicalProcess(GO:"apoptotic process"))

proteinAbundance(HGNC:MYC) increases biologicalProcess(GO:"apoptotic process")
proteinAbundance(HGNC:MYC) increases biologicalProcess(GO:"cell proliferation")

peptidaseActivity(complexAbundance(proteinAbundance(HGNC:F3),proteinAbundance(HGNC:F7))) directlyIncreases peptidaseActivity(proteinAbundance(HGNC:F9))
peptidaseActivity(complexAbundance(proteinAbundance(HGNC:F3),proteinAbundance(HGNC:F7))) directlyIncreases peptidaseActivity(proteinAbundance(HGNC:F10))
peptidaseActivity(proteinAbundance(HGNC:F9)) increases peptidaseActivity(proteinAbundance(HGNC:F2))
peptidaseActivity(proteinAbundance(HGNC:F10)) increases peptidaseActivity(proteinAbundance(HGNC:F2))

SET CellStructure = "Cell Membrane"

abundance(CHEBI:"phosphatidyl-L-serine") increases peptidaseActivity(proteinAbundance(HGNC:F3))
biologicalProcess(GO:"apoptotic process") increases abundance(CHEBI:"phosphatidyl-L-serine")
abundance(CHEBI:"phosphatidyl-L-serine") increases biologicalProcess(GO:coagulation)

proteinAbundance(HGNC:TIMP3) decreases biologicalProcess(GO:angiogenesis)
complexAbundance(proteinAbundance(HGNC:TIMP3),proteinAbundance(HGNC:KDR)) decreases biologicalProcess(GO:angiogenesis)

UNSET STATEMENT_GROUP
